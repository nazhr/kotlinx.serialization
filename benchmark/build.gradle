apply plugin: 'java'
apply plugin: 'kotlin'
apply plugin: 'kotlinx-serialization'
apply plugin: 'idea'
apply plugin: "net.ltgt.apt"
apply plugin: "com.github.johnrengelman.shadow"
apply plugin: "me.champeau.gradle.jmh"

sourceCompatibility = 1.8
targetCompatibility = 1.8

configurations {
    jvmImplementation {
        attributes.attribute(rootProject.ext.jvmCompilerTypeAttr, 'jvm')
    }
    configure([runtimeClasspath, runtimeElements]) {
        extendsFrom jvmImplementation
    }
}

sourceSets.all { sourceSet ->
    sourceSet.compileClasspath += configurations.jvmImplementation
}

jmh.jmhVersion = 1.21

jmhJar {
    baseName 'benchmarks'
    classifier = null
    version = null
}

dependencies {
    attributesSchema {
        attribute(rootProject.ext.jvmCompilerTypeAttr)
    }

    annotationProcessor 'com.google.auto.value:auto-value:1.4.1'
    annotationProcessor 'com.ryanharter.auto.value:auto-value-gson:0.4.6'

    compileOnly 'com.google.auto.value:auto-value:1.4.1'
    compileOnly 'com.ryanharter.auto.value:auto-value-gson:0.4.6'

    implementation "org.openjdk.jmh:jmh-core:1.21"
    implementation 'com.google.guava:guava:24.1.1-jre'

    implementation 'com.squareup.okio:okio:1.13.0'
    implementation 'com.google.code.gson:gson:2.8.1'
    implementation 'com.esotericsoftware:kryo:4.0.0'

    jvmImplementation project(':kotlinx-serialization-core')
    jvmImplementation project(':kotlinx-serialization-protobuf')

    // async profiler
    implementation group: 'pl.project13.scala', name: 'sbt-jmh-extras', version: '0.3.3'
}
